/* src/sysy.lx */
/*  https://pku-minic.github.io/online-doc/#/lv1-main/lexer-parser */
%option noyywrap
%option nounput
%option noinput

/*
CompUnit  ::= FuncDef;

FuncDef   ::= FuncType IDENT "(" ")" Block;
FuncType  ::= "int";

Block         ::= "{" {BlockItem} "}";
BlockItem     ::= Decl | Stmt;

Decl          ::= ConstDecl;
ConstDecl     ::= "const" BType ConstDef {"," ConstDef} ";";
BType         ::= "int";
ConstDef      ::= IDENT "=" ConstInitVal;
ConstInitVal  ::= ConstExp;

Stmt        ::= "return" Exp ";";

Exp         ::= AddExp;
LVal          ::= IDENT;

PrimaryExp    ::= "(" Exp ")" | LVal | Number;
ConstExp      ::= Exp;
Number      ::= INT_CONST;
UnaryExp    ::= PrimaryExp | UnaryOp UnaryExp;
UnaryOp     ::= "+" | "-" | "!";
MulExp      ::= UnaryExp | MulExp ("*" | "/" | "%") UnaryExp;
AddExp      ::= MulExp | AddExp ("+" | "-") MulExp;
RelExp      ::= AddExp | RelExp ("<" | ">" | "<=" | ">=") AddExp;
EqExp       ::= RelExp | EqExp ("==" | "!=") RelExp;
LAndExp     ::= EqExp | LAndExp "&&" EqExp;
LOrExp      ::= LAndExp | LOrExp "||" LAndExp;
 */

%{
#include <string>
#include <cstdlib>
#include "sysy.tab.hpp"
%}

/* White space and comment */
WhiteSpace [ \t\n\r]*
LineComment "//".*
%x MultiLineComment
/* identifier */
Identifier [a-zA-Z_][a-zA-Z0-9_]*

/* integer literal */
Decimal [1-9][0-9]*
Octal 0[0-7]*
Hexadecimal 0[xX][0-9a-fA-F]+

%%
{WhiteSpace}        {}
{LineComment}       {}

"/*"                { BEGIN(MultiLineComment); }
<MultiLineComment>{
    "*/"            { BEGIN(INITIAL); } /* return to initialize state */
    [^*\n]+         { /* ignore chars that not * and not newline */ }
    "*"             { /* ignore single * */ }
    \n              { /* ignore newline */ }
}

"int"               { return INT; }
"return"            { return RETURN; }
"void"              { return VOID; }
"const"             { return CONST; }

{Identifier}        { yylval.str_val = new std::string(yytext); return IDENT; }

{Decimal}           { yylval.int_val = std::strtol(yytext, nullptr, 0); return INT_CONST; }
{Octal}             { yylval.int_val = std::strtol(yytext, nullptr, 0); return INT_CONST; }
{Hexadecimal}       { yylval.int_val = std::strtol(yytext, nullptr, 0); return INT_CONST; }

"&&"                { return AND; }
"||"                { return OR; }
"<="                { return LE; }
">="                { return GE; }
"=="                { return EQ; }
"!="                { return NE; }

.                   { return yytext[0]; }
%%